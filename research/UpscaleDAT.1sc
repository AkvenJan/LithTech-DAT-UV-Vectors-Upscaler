// UpscaleDAT: Fix UV stuff for NOLF maps, for use with upscaled textures.
// 
// Asks for the scaling factor, files to process (use multiple selection) and the path to save the processed files in.
// Files in the output dir may be overwritten without warning! Make sure the template is installed.


// Misc setup and user input.

const int MaxChar = 1024;

const char Template[MaxChar] = "bspv66.bt"; 

double ScaleFactor = 2.0;
do {
  ScaleFactor = InputFloat("Scaling factor", "Enter the scaling factor to use", "4.0");
}
while ((int) ScaleFactor == BAD_VALUE);

const TOpenFileNames InFiles = InputOpenFileNames("Select files to fix", "NOLF map files (*.dat)|All files (*)");

const char OutDir[MaxChar] = InputDirectory("Select output folder.");

// Process the files.

int Errors = 0;
int FileNum;
int Status;
char InName[MaxChar];
char BaseName[MaxChar];
char OutName[MaxChar];

int ModelCnt, ModelNum;
int SurfCnt, SurfNum;

for(FileNum = 0; FileNum < InFiles.count; ++FileNum) {

  // Open and parse the file.
  
//  Strcpy(InName, InFiles.file[FileNum].filename);
  InName = InFiles.file[FileNum].filename;

  // Opens a file separately even if it is open in the editor.
  // Does not run the associated template here because that did not work...
  Status = FileOpen(InName, false, "", true);
  if (Status < 0) {
    prinf("ERROR: Failed to open file %s for reading.", InName);
    ++Errors;
    continue;
  }
  // ...instead run the template explicitly.
  RunTemplate(Template);

  // Do the stuff...
  
  ModelCnt = modelHeader.Count;

  for (ModelNum = 0; ModelNum < ModelCnt; ++ModelNum) {

    SurfCnt = modelHeader.model[ModelNum].BSPData.SurfaceCount;
  
    for (SurfNum = 0; SurfNum < SurfCnt; ++SurfNum) {
  
      modelHeader.model[ModelNum].BSPData.Surfaces[SurfNum].UV2.x *= ScaleFactor;  
      modelHeader.model[ModelNum].BSPData.Surfaces[SurfNum].UV2.y *= ScaleFactor;  
      modelHeader.model[ModelNum].BSPData.Surfaces[SurfNum].UV2.z *= ScaleFactor;  

      modelHeader.model[ModelNum].BSPData.Surfaces[SurfNum].UV3.x *= ScaleFactor;  
      modelHeader.model[ModelNum].BSPData.Surfaces[SurfNum].UV3.y *= ScaleFactor;  
      modelHeader.model[ModelNum].BSPData.Surfaces[SurfNum].UV3.z *= ScaleFactor;  
  
    }

  }

  // Save under the same name in the output dir and clean up.

  BaseName = FileNameGetBase(InName);
  OutName = OutDir;
  OutName += BaseName;

  Status = FileSave(OutName);
  if (Status < 0) {
    prinf("ERROR: Failed to write the file %s.", OutName);
    ++Errors;
  }

  FileClose();

}

// Status report.

if (Errors) {
  MessageBox(idOk, "Ouch...", "%d errors encountered during processing.\nPlease check the output window.", Errors);
}
else {
  MessageBox(idOk, "Phew...", "Processing finished without errors.");
}
